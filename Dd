<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:xs="http://www.w3.org/2001/XMLSchema">
  <xsl:output method="html" />

  <xsl:template match="/">
    <html>
      <head>
        <title>XSD Documentation</title>
      </head>
      <body>
        <xsl:apply-templates />
      </body>
    </html>
  </xsl:template>

  <xsl:template match="xs:complexType">
    <xsl:if test="count(xs:sequence/xs:element) &gt; 1">
      <h2>
        <xsl:value-of select="@name" />
        Table
      </h2>
      <table>
        <thead>
          <tr>
            <th>Element Name</th>
            <th>Type</th>
            <th>CARDINALITY</th>
            <th>Restrictions</th>
            <th>Attributes</th>
            <th>Comments</th>
          </tr>
        </thead>
        <tbody>
          <xsl:apply-templates select="xs:sequence/xs:element"/>
        </tbody>
      </table>
    </xsl:if>
  </xsl:template>

  <xsl:template match="xs:element">
    <tr>
      <td><xsl:value-of select="@name" /></td>
      <td><xsl:value-of select="@type" /></td>
      <td><xsl:value-of select="@minOccurs" /> - <xsl:value-of select="@maxOccurs" /></td>
      <td><xsl:apply-templates select="xs:simpleType/xs:restriction"/></td>
      <td>
        <xsl:apply-templates select="xs:complexType/xs:attribute"/>
      </td>
      <td><xsl:apply-templates select="xs:annotation/xs:documentation"/></td>
    </tr>
    <xsl:apply-templates select="xs:complexType/xs:sequence/xs:element"/>
  </xsl:template>

  <xsl:template match="xs:simpleType/xs:restriction">
    <xsl:value-of select="@base" />
    <xsl:if test="xs:enumeration">
      <br />
      <xsl:text>Enumeration: </xsl:text>
      <xsl:for-each select="xs:enumeration">
        <xsl:value-of select="@value" />
        <xsl:if test="position() != last()">
          <xsl:text>, </xsl:text>
        </xsl:if>
      </xsl:for-each>
    </xsl:if>
  </xsl:template>

  <xsl:template match="xs:complexType/xs:attribute">
    <xsl:value-of select="@name" />
    <xsl:if test="position() != last()">
      <xsl:text>, </xsl:text>
    </xsl:if>
  </xsl:template>

</xsl:stylesheet>
